Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, ОписаниеТипов, Заголовок = "", РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт  
	
	ПараметрыГруппы = А1Э_Структуры.Скопировать(Параметры);
	ПараметрыГруппы.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	
	ДействияПолей = А1Э_Структуры.Скопировать(Действия);
	ДействияКомпонента = Новый Структура;
	Для Каждого Пара Из ДействияПолей Цикл
		Если СтрНачинаетсяС(Пара.Ключ, "Форма") Тогда
			ДействияКомпонента.Вставить(Пара.Ключ, Пара.Значение);
		КонецЕсли;
	КонецЦикла;
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикКДействиям(ДействияКомпонента, "ФормаОбновитьСвойстваЭлементов", "А1БК_ПолеВыбораНескольких.ФормаОбновитьСвойстваЭлементов");
	А1Э_Формы.ДобавитьОписаниеГруппы(МассивОписаний, ИмяКомпонента, , РодительЭлемента, ПередЭлементом, ПараметрыГруппы, ДействияКомпонента); 
	
	Для Каждого Пара Из ДействияКомпонента Цикл
		ДействияПолей.Удалить(Пара.Ключ);
	КонецЦикла;
	А1Э_УниверсальнаяФорма.ВставитьОбработчикВДействия(ДействияПолей, "ПриИзменении", 0, "А1БК_ПолеВыбораНескольких.ПриИзменении"); 
	
	МассивЗначений = А1Э_Массивы.Массив(А1Э_Иерархии.ЗначениеСвойства(Параметры, "ЗначениеРеквизита"));
	ПоказыватьНесколько = МассивЗначений.Количество() > 1;
	
	ПараметрыОдного = А1Э_Структуры.СкопироватьВШаблон(Параметры,
	"ЗначениеРеквизита", А1Э_Иерархии.ЗначениеСвойства(МассивЗначений, 0),
	"КнопкаОчистки", Истина);
	А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяОдного(ИмяКомпонента), ОписаниеТипов, , А1Э_Общее.НепустоеЗначение(Заголовок, ИмяКомпонента), ИмяКомпонента, , ПараметрыОдного, ДействияПолей); 
	
	ПараметрыНескольких = А1Э_Структуры.СкопироватьВШаблон(Параметры,
	"ТипЗначенияСписка", ОписаниеТипов);
	А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяНескольких(ИмяКомпонента), "СписокЗначений", , А1Э_Общее.НепустоеЗначение(Заголовок, ИмяКомпонента), ИмяКомпонента, , ПараметрыНескольких, ДействияПолей);
	
	А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяПереключателя(ИмяКомпонента), "Булево", , "Несколько", ИмяКомпонента, ,
	А1Э_Структуры.Создать(
	"ЗначениеРеквизита", ПоказыватьНесколько),
	А1Э_Структуры.Создать(
	"ПриИзменении", "А1БК_ПолеВыбораНескольких.ПереключательВариантовВыбораПриИзменении",
	));
КонецФункции

Функция МассивЗначений(Форма, ИмяКомпонента) Экспорт
	Если Форма[ИмяПереключателя(ИмяКомпонента)] = Истина Тогда 
		Возврат Форма[ИмяНескольких(ИмяКомпонента)].ВыгрузитьЗначения();
	Иначе
		Значение = Форма[ИмяОдного(ИмяКомпонента)];
		Если Не ЗначениеЗаполнено(Значение) Тогда Возврат Новый Массив; КонецЕсли;
		Возврат А1Э_Массивы.Создать(Значение);
	КонецЕсли;
КонецФункции 

Функция УстановитьЗначение(Форма, ИмяКомпонента, Знач Значение) Экспорт
	Значение = А1Э_Массивы.Массив(Значение);
	Форма[ИмяОдного(ИмяКомпонента)] = А1Э_Иерархии.ЗначениеСвойства(Значение, 0);
	Форма[ИмяНескольких(ИмяКомпонента)].ЗагрузитьЗначения(Значение);
	Форма[ИмяПереключателя(ИмяКомпонента)] = Значение.Количество() > 1;
	
	ФормаОбновитьСвойстваЭлементов(ИмяКомпонента, Форма);
КонецФункции

#Область Служебно 

Функция ПриИзменении(Форма, Элемент) Экспорт
	ИмяКомпонента = А1Э_УниверсальнаяФорма.ИмяКомпонента(Элемент);
	ИмяОдного = ИмяОдного(ИмяКомпонента);
	ИмяНескольких = ИмяНескольких(ИмяКомпонента); 
	
	Если Элемент.Имя = ИмяОдного Тогда
		Форма[ИмяНескольких].Очистить();
		Если ЗначениеЗаполнено(Форма[ИмяОдного]) Тогда
			Форма[ИмяНескольких].ЗагрузитьЗначения(А1Э_Массивы.Массив(Форма[ИмяОдного]));
		КонецЕсли;
	Иначе
		Форма[ИмяОдного] = ?(Форма[ИмяНескольких].Количество() > 0, Форма[ИмяНескольких][0].Значение, Неопределено);
	КонецЕсли;
КонецФункции

Функция ПереключательВариантовВыбораПриИзменении(Форма, Элемент) Экспорт 
	ФормаОбновитьСвойстваЭлементов(А1Э_УниверсальнаяФорма.ИмяКомпонента(Элемент), Форма);
КонецФункции

Функция ФормаОбновитьСвойстваЭлементов(ИмяКомпонента, Форма) Экспорт
	Форма.Элементы[ИмяОдного(ИмяКомпонента)].Видимость = НЕ Форма[ИмяПереключателя(ИмяКомпонента)];
	Форма.Элементы[ИмяНескольких(ИмяКомпонента)].Видимость = Форма[ИмяПереключателя(ИмяКомпонента)];
КонецФункции 

Функция ИмяОдного(ИмяКомпонента)
	Возврат ИмяКомпонента + "___ВыборОдного";
КонецФункции

Функция ИмяНескольких(ИмяКомпонента) 
	Возврат ИмяКомпонента + "___ВыборНескольких";
КонецФункции

Функция ИмяПереключателя(ИмяКомпонента)
	Возврат ИмяКомпонента + "___ПереключательВариантовВыбора";
КонецФункции

#КонецОбласти

