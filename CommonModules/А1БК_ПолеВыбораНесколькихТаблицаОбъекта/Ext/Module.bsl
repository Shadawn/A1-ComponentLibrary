#Если НЕ Клиент Тогда
	
	// Показывает пользователю ПолеВыбораНескольких. Данные этого поля записываются в одноименную табличную часть объекта в колонку "Значение".
	//
	// Параметры:
	//  МассивОписаний	 - Массив - 
	//  ИмяКомпонента	 - Строка - должно совпадать с именем табличной части объекта
	//  Форма			 - ФормаКлиентскогоПриложения - 
	//  Заголовок		 - 	 - 
	//  РодительЭлемента - 	 - 
	//  ПередЭлементом	 - 	 - 
	//  Параметры		 - 	 - 
	//  Действия		 - 	 - 
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, Форма, Заголовок = "", РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
		ОписаниеТипов = А1Э_Метаданные.ОписаниеТипаКолонки(Форма.Объект.Ссылка, ИмяКомпонента, "Значение");
		
		ДобавитьОписание__Внутренний(МассивОписаний, ИмяКомпонента, ОписаниеТипов, Ложь, Заголовок, РодительЭлемента, ПередЭлементом, Параметры, Действия);
	КонецФункции
	
	// Функция - Добавить описание по идентификаторам
	//
	// Параметры:
	//  МассивОписаний	 - 	 - 
	//  ИмяКомпонента	 - 	 - 
	//  ОписаниеТипов	 - 	 - 
	//  Заголовок		 - 	 - 
	//  РодительЭлемента - 	 - 
	//  ПередЭлементом	 - 	 - 
	//  Параметры		 - 	 - 
	//  Действия		 - 	 - 
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция ДобавитьОписаниеПоИдентификаторам(МассивОписаний, ИмяКомпонента, ОписаниеТипов, Заголовок = "", РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт   
		ДобавитьОписание__Внутренний(МассивОписаний, ИмяКомпонента, ОписаниеТипов, Истина, Заголовок, РодительЭлемента, ПередЭлементом, Параметры, Действия);			
	КонецФункции

	Функция ДобавитьОписание__Внутренний(МассивОписаний, ИмяКомпонента, ОписаниеТипов, ПоИдентификаторам, Заголовок = "", РодительЭлемента, ПередЭлементом, Параметры, Действия)   
		РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
		"СохраняемыеДанные", Истина,
		);
		РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
		
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПриСозданииНаСервере", 0);
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПередЗаписьюНаСервере", 0);
		
		А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, А1Э_Структуры.Создать(
		"ПоИдентификаторам", ПоИдентификаторам,
		)); 
		А1БК_ПолеВыбораНескольких.ДобавитьОписание(МассивОписаний, ИмяКомпонента, ОписаниеТипов, Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, РабочиеДействия);
	
	КонецФункции
	
	Функция ФормаПриСозданииНаСервере(ИмяКомпонента, Форма, Отказ, СтандартнаяОбработка) Экспорт
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);

		МассивЗначений = Новый Массив;
		Для Каждого Строка Из Форма.Объект[ИмяКомпонента] Цикл
			Если Настройки.ПоИдентификаторам Тогда
				Значение = А1Э_Метаданные.СсылкаПоИдентификатору(Строка.Значение);
			Иначе
				Значение = Строка.Значение;
			КонецЕсли;
			МассивЗначений.Добавить(Значение);
		КонецЦикла;
		А1БК_ПолеВыбораНескольких.УстановитьЗначение(Форма, ИмяКомпонента, МассивЗначений);
	КонецФункции 	
	
	Функция ФормаПередЗаписьюНаСервере(ИмяКомпонента, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт 
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		
		МассивЗначений = А1БК_ПолеВыбораНескольких.МассивЗначений(Форма, ИмяКомпонента);
		ТекущийОбъект[ИмяКомпонента].Очистить();
		Для Каждого Значение Из МассивЗначений Цикл
			Строка = ТекущийОбъект[ИмяКомпонента].Добавить();
			Если Настройки.ПоИдентификаторам Тогда
				Строка.Значение = А1Э_Метаданные.ИдентификаторПоСсылке(Значение);
			Иначе
				Строка.Значение = Значение;
			КонецЕсли;
		КонецЦикла;
	КонецФункции
	
#КонецЕсли 

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_ПолеВыбораНесколькихТаблицаОбъекта";
КонецФункции 