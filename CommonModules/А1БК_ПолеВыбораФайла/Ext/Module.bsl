Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, Заголовок = "", ПараметрыДиалога = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
	"КнопкаВыбора", Истина,
	);
	
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	ДобавитьОбработчикКДеййствиям(РабочиеДействия);
	
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента,
	А1Э_Структуры.Создать(
	"ПараметрыДиалога", ПараметрыДиалога,
	));
	А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяКомпонента, "Строка", , Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, РабочиеДействия);
КонецФункции

Функция ДобавитьОбработчикКДеййствиям(Действия) Экспорт
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(Действия, ИмяМодуля(), "НачалоВыбора:НаКлиентеАсинхронно", 0);
КонецФункции

Функция ДобавитьОбработчикЭлементаФормы(Форма, Элемент, ПараметрыДиалога = Неопределено) Экспорт
	А1Э_УниверсальнаяФорма.ВставитьВНастройкиКомпонента(Форма, Элемент.Имя, А1Э_Структуры.Создать(
	"ПараметрыДиалога", ПараметрыДиалога,
	));
	Элемент.КнопкаВыбора = Истина;
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикЭлементаФормы(Форма, Элемент, "НачалоВыбора", ИмяМодуля() + ".НачалоВыбора:НаКлиентеАсинхронно", 0);
КонецФункции

#Если Клиент Тогда
	Функция НачалоВыбора(АсинхронныйКонтекст, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт 
		СтандартнаяОбработка = Ложь;
		АсинхронныйКонтейнер = А1Э_УниверсальнаяФорма.АсинхронныйКонтейнер(АсинхронныйКонтекст, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка);
		
		НастройкиКомпонента = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, Элемент.Имя);
		Контекст = А1Э_Структуры.Скопировать(НастройкиКомпонента.ПараметрыДиалога);
		Контекст.Вставить("АсинхронныйКонтейнер", АсинхронныйКонтейнер);
		//ТУДУ: Сделать корректную обработку ситуации когда имя колонки построено не по шаблону.
		ТаблицаЭлемента = А1Э_Формы.ТаблицаЭлемента(Форма, Элемент);
		Если ТаблицаЭлемента = Неопределено Тогда
			Контекст.Вставить("КонтейнерДанных", Форма);
			Контекст.Вставить("ИмяДанных", Элемент.Имя);
		Иначе
			Контекст.Вставить("КонтейнерДанных", ТаблицаЭлемента.ТекущиеДанные);
			Контекст.Вставить("ИмяДанных", А1Э_Строки.После(Элемент.Имя, ТаблицаЭлемента.Имя));
		КонецЕсли;
		
		А1Э_Файлы.ПоказатьВыборФайла("НачалоВыбораЗавершение", ЭтотОбъект, Контекст);
	КонецФункции
	
	Функция НачалоВыбораЗавершение(Результат, Контекст) Экспорт
		Если ТипЗнч(Результат) = Тип("Массив") Тогда
			Контекст.КонтейнерДанных[Контекст.ИмяДанных] = Результат[0];
		КонецЕсли;
		
		А1Э_УниверсальнаяФорма.ЗавершитьАсинхронныйОбработчик(Контекст.АсинхронныйКонтейнер);
	КонецФункции 
#КонецЕсли

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_ПолеВыбораФайла";	
КонецФункции 