// Создает поле с кнопкой выбора, которая при нажатии открывает диалог выбора файла. При выборе в диалоге значение записывается в реквизит, связанный с полем.
//
// Параметры:
//  МассивОписаний	 - Массив - 
//  ИмяКомпонента	 - Строка - 
//  Заголовок		 - Строка - 
//  ПараметрыДиалога - Структура,Неопределено - может иметь ключи Режим, ПолноеИмяФайла, Заголовок, Фильтр, Расширение. Эти ключи повлияют на работу диалога выбора файла. 
//    В качестве значений необходимо использовать строки (т. е. "ВыборКаталога" вместо РежимДиалогаВыбораФайла.ВыборКаталога). 
//  РодительЭлемента - ЭлементФормы,Строка,Неопределено - 
//  ПередЭлементом	 - ЭлементФормы,Строка,Неопределено - 
//  Параметры		 - Структура,Неопределено - 
//  Действия		 - Структура,Неопределено - 
// 
// Возвращаемое значение:
//   - 
//
Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, Заголовок = "", ПараметрыДиалога = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	Возврат ДобавитьПроизвольноеОписание(МассивОписаний, ИмяКомпонента, Истина, "", Заголовок, ПараметрыДиалога, РодительЭлемента, ПередЭлементом, Параметры, Действия);
КонецФункции

Функция ДобавитьОписаниеПоляОбъекта(МассивОписаний, Имя, ПараметрыДиалога = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт  
	Возврат ДобавитьПроизвольноеОписание(МассивОписаний, Имя, Ложь, "Объект", "", ПараметрыДиалога, РодительЭлемента, ПередЭлементом, Параметры, Действия); 
КонецФункции 

Функция ДобавитьОбработчикКДействиям(Действия) Экспорт
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(Действия, ИмяМодуля(), "НачалоВыбора:НаКлиентеАсинхронно", 0);
КонецФункции

Функция ДобавитьОбработчикЭлементаФормы(Форма, Элемент, ПараметрыДиалога = Неопределено) Экспорт
	А1Э_УниверсальнаяФорма.ВставитьВНастройкиКомпонента(Форма, Элемент.Имя, А1Э_Структуры.Создать(
	"А1БК_ПараметрыДиалога", ПараметрыДиалога,
	"А1БК_ПутьКДанным", Элемент.ПутьКДанным,
	));
	Элемент.КнопкаВыбора = Истина;
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикЭлементаФормы(Форма, Элемент, "НачалоВыбора", ИмяМодуля() + ".НачалоВыбора:НаКлиентеАсинхронно", 0);
КонецФункции

#Область Служебно

Функция ДобавитьПроизвольноеОписание(МассивОписаний, ИмяКомпонента, СоздаватьРеквизит, ПутьКДанным, Заголовок, ПараметрыДиалога, РодительЭлемента, ПередЭлементом, Параметры, Действия)
	РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
	"КнопкаВыбора", Истина,
	);
	
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	ДобавитьОбработчикКДействиям(РабочиеДействия);
	
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента,
	А1Э_Структуры.Создать(
	"А1БК_ПараметрыДиалога", ПараметрыДиалога,
	"А1БК_ПутьКДанным", ПутьКДанным, 
	));
	Если СоздаватьРеквизит Тогда
		Возврат А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяКомпонента, "Строка", ПутьКДанным, Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, РабочиеДействия);
	Иначе
		Возврат А1Э_Формы.ДобавитьОписаниеЭлемента(МассивОписаний, ИмяКомпонента, ПутьКДанным, Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, РабочиеДействия);
	КонецЕсли;
КонецФункции 

#Если Клиент Тогда
	
	Функция НачалоВыбора(АсинхронныйКонтекст, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт 
		СтандартнаяОбработка = Ложь;
		АсинхронныйКонтейнер = А1Э_УниверсальнаяФорма.АсинхронныйКонтейнер(АсинхронныйКонтекст, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка);
		
		НастройкиКомпонента = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, Элемент.Имя);
		ПутьКДанным = НастройкиКомпонента.А1БК_ПутьКДанным;
		Контекст = А1Э_Структуры.Скопировать(НастройкиКомпонента.А1БК_ПараметрыДиалога);
		Контекст.Вставить("АсинхронныйКонтейнер", АсинхронныйКонтейнер);
		//ТУДУ: Сделать корректную обработку ситуации когда имя колонки построено не по шаблону.
		//ТУДУ: Сделать корректную обработку таблиц объекта.
		ТаблицаЭлемента = А1Э_Формы.ТаблицаЭлемента(Форма, Элемент);
		Если ТаблицаЭлемента = Неопределено Тогда
			Если ПутьКДанным = "" Тогда
				Контекст.Вставить("КонтейнерДанных", Форма);
			Иначе
				Контекст.Вставить("КонтейнерДанных", Форма[ПутьКДанным]);
			КонецЕсли;
			Контекст.Вставить("ИмяДанных", Элемент.Имя);
		Иначе
			Контекст.Вставить("КонтейнерДанных", ТаблицаЭлемента.ТекущиеДанные);
			Контекст.Вставить("ИмяДанных", А1Э_Строки.После(Элемент.Имя, ТаблицаЭлемента.Имя));
		КонецЕсли;
		
		А1Э_Файлы.ПоказатьВыборФайла("НачалоВыбораЗавершение", ЭтотОбъект, Контекст);
	КонецФункции
	
	Функция НачалоВыбораЗавершение(Результат, Контекст) Экспорт
		Если ТипЗнч(Результат) = Тип("Массив") Тогда
			Контекст.КонтейнерДанных[Контекст.ИмяДанных] = Результат[0];
		КонецЕсли;
		
		А1Э_УниверсальнаяФорма.ЗавершитьАсинхронныйОбработчик(Контекст.АсинхронныйКонтейнер);
	КонецФункции 
	
#КонецЕсли

#КонецОбласти 

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_ПолеВыбораФайла";	
КонецФункции 