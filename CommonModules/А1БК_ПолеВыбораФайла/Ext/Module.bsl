Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, Заголовок = "", ПараметрыДиалога = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
	"КнопкаВыбора", Истина,
	);
	
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "НачалоВыбора:НаКлиентеАсинхронно", 0);
	
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента,
	А1Э_Структуры.Создать(
	"ПараметрыДиалога", ПараметрыДиалога,
	));
	А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяКомпонента, "Строка", , Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, РабочиеДействия);
КонецФункции

#Если Клиент Тогда
	Функция НачалоВыбора(АсинхронныйКонтекст, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт 
		СтандартнаяОбработка = Ложь;
		АсинхронныйКонтейнер = А1Э_УниверсальнаяФорма.АсинхронныйКонтейнер(АсинхронныйКонтекст, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка);
		
		НастройкиКомпонента = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, Элемент.Имя);
		Контекст = А1Э_Структуры.Скопировать(НастройкиКомпонента.ПараметрыДиалога);
		Контекст.Вставить("АсинхронныйКонтейнер", АсинхронныйКонтейнер);
		
		А1Э_Файлы.ПоказатьВыборФайла("НачалоВыбораЗавершение", ЭтотОбъект, Контекст);
	КонецФункции
	
	Функция НачалоВыбораЗавершение(Результат, Контекст) Экспорт
		Если ТипЗнч(Результат) = Тип("Массив") Тогда
			Контекст.АсинхронныйКонтейнер.Форма[Контекст.АсинхронныйКонтейнер.Элемент.Имя] = Результат[0];
		КонецЕсли;
		
		А1Э_УниверсальнаяФорма.ЗавершитьАсинхронныйОбработчик(Контекст.АсинхронныйКонтейнер);
	КонецФункции 
#КонецЕсли

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_ПолеВыбораФайла";	
КонецФункции 