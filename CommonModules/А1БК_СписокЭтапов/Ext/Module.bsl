Функция ДобавитьОписание(
	МассивОписаний, 
	ИмяКомпонента = "СписокЭтапов", 
	СписокВыбора = Неопределено,
	РодительЭлемента = Неопределено,
	ПередЭлементом = Неопределено,
	Параметры = Неопределено,
	Действия = Неопределено
	) Экспорт
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	Если НЕ СписокВыбора = Неопределено Тогда
		Параметры.Вставить("СписокВыбора", СписокВыбора);
	КонецЕсли;
	Если НЕ РодительЭлемента = Неопределено Тогда
		Параметры.Вставить("РодительЭлемента", РодительЭлемента);
	КонецЕсли;
	Если НЕ ПередЭлементом = Неопределено Тогда
		Параметры.Вставить("ПередЭлементом", ПередЭлементом);
	КонецЕсли;
	Если НЕ Действия = Неопределено Тогда
		Параметры.Вставить("Действия", Действия);
	КонецЕсли;
	
	НастройкиКомпонента = Новый Структура;
	ЗаполнениеНастроек = А1БК_Общее.ЗаполнитьНастройки(НастройкиКомпонента, Параметры, ИмяМодуля());
	Если НЕ ЗаполнениеНастроек = Неопределено Тогда
		Сообщить("Ошибка заполнения параметра " + ЗаполнениеНастроек);
		Возврат Неопределено;
	КонецЕсли;
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, НастройкиКомпонента);
	
	ДобавитьОписаниеГруппы(МассивОписаний, ИмяКомпонента, НастройкиКомпонента);
	ДобавитьОписаниеСписка(МассивОписаний, ИмяКомпонента, НастройкиКомпонента);
	ДобавитьЗаписьЭлемента(МассивОписаний, ИмяКомпонента, НастройкиКомпонента);
	Если НастройкиКомпонента.ОтображатьШкалу Тогда
		ДобавитьОписаниеШкалы(МассивОписаний, ИмяКомпонента, НастройкиКомпонента);
	КонецЕсли;
КонецФункции

#Область Описания
Функция ДобавитьОписаниеЖурнала(МассивОписаний, ИмяКомпонента, НастройкиКомпонента) Экспорт
	А1Э_Формы.ДобавитьОписаниеРеквизита(
	МассивОписаний,
	"А1БК_СписокЭтапов_Журнал",
	"",
	"",
	"",
	А1Э_Структуры.Создать(
	"ЗначениеРеквизита", А1Э_Структуры.Создать(
	"Элементы", Новый Структура,
	"ПриОткрытииЗавершена", Ложь
	)
	)
	);
КонецФункции

Функция ДобавитьЗаписьЭлемента(МассивОписаний, ИмяКомпонента, НастройкиКомпонента) Экспорт
	Последний = МассивОписаний[МассивОписаний.Количество() - 1];
	Если Последний.Имя = "А1БК_СписокЭтапов_Журнал" Тогда
		Журнал = Последний;
	Иначе
		Журнал = Неопределено;
		Для Индекс = 0 По МассивОписаний.Количество() - 2 Цикл
			Если МассивОписаний[Индекс].Имя = "А1БК_СписокЭтапов_Журнал" Тогда
				Журнал = МассивОписаний[Индекс];
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если Журнал = Неопределено Тогда
		ДобавитьОписаниеЖурнала(МассивОписаний, ИмяКомпонента, НастройкиКомпонента);
		Журнал = МассивОписаний[МассивОписаний.Количество() - 1];
	КонецЕсли;
	
	Имя = А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "ИмяРеквизита");
	Если Имя = Неопределено Тогда
		Имя = ИмяКомпонента + "___Этап";
	КонецЕсли;
	Элементы = Журнал.Параметры.ЗначениеРеквизита.Элементы;
	Элементы.Вставить(Имя, ИмяКомпонента);		
КонецФункции

Функция ДобавитьОписаниеГруппы(МассивОписаний, ИмяКомпонента, НастройкиКомпонента) Экспорт
	А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(
	МассивОписаний, 
	ИмяКомпонента + "___Группа",
	"",
	А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "РодительЭлемента"),
	А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "ПередЭлементом"),
	А1Э_Структуры.Создать(
	"ОтображатьЗаголовок", Ложь,
	"Отображение", ОтображениеОбычнойГруппы.Нет
	)
	);
КонецФункции

Функция ДобавитьОписаниеСписка(МассивОписаний, ИмяКомпонента, НастройкиКомпонента) Экспорт
	Действия = А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "Действия", Новый Структура);
	Если А1Э_Структуры.Свойство(Действия, "ПриИзменении") Тогда
		Действия.ПриИзменении = Действия.ПриИзменении + ", " + ИмяМодуля() + ".ПриИзменении";
	Иначе
		Действия.Вставить("ПриИзменении", ИмяМодуля() + ".ПриИзменении");
	КонецЕсли;
	Если А1Э_Структуры.Свойство(Действия, "ФормаПриОткрытии") Тогда
		Действия.ФормаПриОткрытии = Действия.ПриИзменении + ", " + ИмяМодуля() + ".ФормаПриОткрытии";
	Иначе
		Действия.Вставить("ФормаПриОткрытии", ИмяМодуля() + ".ФормаПриОткрытии");
	КонецЕсли;
	ИмяРеквизита = А1Э_Общее.ЗначениеСвойства(НастройкиКомпонента, "ИмяРеквизита");
	Параметры = А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "ПараметрыСписка", Новый Структура);
	Параметры.Вставить("СписокВыбора", НастройкиКомпонента.СписокВыбора);
	Параметры.Вставить("РежимВыбораИзСписка", Истина);
	Параметры.Вставить("КнопкаВыпадающегоСписка", НЕ НастройкиКомпонента.Заморозка);
	Параметры.Вставить("КнопкаВыбора", Ложь);
	Параметры.Вставить("ТолькоПросмотр", НастройкиКомпонента.Заморозка);
	Если ИмяРеквизита = Неопределено Тогда
		А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(
		МассивОписаний, 
		ИмяКомпонента + "___Этап",
		А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "ОписаниеТипов", ""),
		,
		А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "Заголовок", "Этап"),
		ИмяКомпонента + "___Группа",
		,
		Параметры,
		Действия
		);
	Иначе
		ПутьКРеквизиту = А1Э_Общее.ЗначениеСвойства(НастройкиКомпонента, "ПутьКРеквизиту");
		А1Э_Формы.ДобавитьОписаниеЭлемента(
		МассивОписаний,
		ИмяРеквизита,
		?(ПутьКРеквизиту = Неопределено, "", ПутьКРеквизиту),
		НастройкиКомпонента.Заголовок,
		ИмяКомпонента + "___Группа",
		,
		Параметры,
		Действия
		);
	КонецЕсли;
КонецФункции

Функция ДобавитьОписаниеШкалы(МассивОписаний, ИмяКомпонента, НастройкиКомпонента) Экспорт
	ПараметрыШкалы = А1Э_Структуры.ЗначениеСвойства(
	НастройкиКомпонента, 
	"ПараметрыШкалы", 
	Новый Структура
	);
	ПараметрыШкалы.Вставить(
	"ЧислоШагов", НастройкиКомпонента.СписокВыбора.Количество()
	);
	Если НЕ А1Э_Структуры.Свойство(ПараметрыШкалы, "ШиринаГраницы") Тогда
		ПараметрыШкалы.Вставить("ШиринаГраницы", 1.2);
	КонецЕсли;
	Если А1Э_Общее.Свойство(НастройкиКомпонента, "ЗначениеРеквизита") Тогда
		Данные = НастройкиКомпонента.СписокВыбора;
		Индекс = Данные.Найти(НастройкиКомпонента.ЗначениеРеквизита);
		Заполнение = ?(Данные.Количество > 1, (Индекс + 1) / Данные.Количество(), 1);
		ПараметрыШкалы.Вставить("Заполнение", Заполнение);
	КонецЕсли;
	А1БК_ШкалаПрогресса.ДобавитьОписание(
	МассивОписаний, 
	ИмяКомпонента + "Шкала",
	ИмяКомпонента + "___Группа",
	,
	ПараметрыШкалы
	);
КонецФункции
#КонецОбласти

#Область Обработчики
Функция ПриИзменении(Форма, Элемент) Экспорт
	ИмяКомпонента = Форма.А1БК_СписокЭтапов_Журнал.Элементы[Элемент.Имя];
	НастройкиКомпонента = Форма[ИмяКомпонента + "___Настройки"];
	Список = НастройкиКомпонента.СписокВыбора;
	ПутьКРеквизиту = А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "ПутьКРеквизиту");
	ТекущийЭтап = ?(ПутьКРеквизиту = Неопределено, Форма[Элемент.Имя], Форма[ПутьКРеквизиту][Элемент.Имя]);
	Индекс = Список.Найти(ТекущийЭтап);
	НастройкиКомпонента.Вставить("Индекс", Индекс);
	
	Если НЕ НастройкиКомпонента.ОтображатьШкалу Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Заполнение = (Индекс + 1) / Список.Количество();
	А1БК_ШкалаПрогресса.ОбновитьШкалу(Форма, ИмяКомпонента + "Шкала", Заполнение);
КонецФункции

Функция ФормаПриОткрытии(Элемент, Форма, Отказ) Экспорт
	Журнал = Форма.А1БК_СписокЭтапов_Журнал;
	Если Журнал.ПриОткрытииЗавершена Тогда
		Возврат Неопределено;
	КонецЕсли;
	Для Каждого Пара Из Журнал.Элементы Цикл
		Настройки = Форма[Пара.Значение + "___Настройки"];
		Если НЕ Настройки.ОтображатьШкалу Тогда
			Продолжить;
		КонецЕсли;
		ПутьКРеквизиту = А1Э_Структуры.ЗначениеСвойства(Настройки, "ПутьКРеквизиту");
		Значение = ?(ПутьКРеквизиту = Неопределено, Форма[Пара.Ключ], Форма[ПутьКРеквизиту][Пара.Ключ]);
		Список = Настройки.СписокВыбора;
		Индекс = Список.Найти(Значение);
		Заполнение = (Индекс + 1) / Список.Количество();
		НастройкиШкалы = Форма[Пара.Значение + "Шкала___Настройки"];
		А1БК_ШкалаПрогресса.ОбновитьШкалу(Форма, Пара.Значение + "Шкала", Заполнение);
	КонецЦикла;
	Журнал.Вставить("ПриОткрытииЗавершена", Истина);
КонецФункции
#КонецОбласти

#Область Общее
Функция ИмяМодуля() Экспорт
	Возврат "А1БК_СписокЭтапов";
КонецФункции

Функция ОбязательныеПараметры() Экспорт
	Возврат А1Э_Массивы.Создать(
		"СписокВыбора"
	);
КонецФункции

Функция ДополнительныеПараметры() Экспорт
	Возврат А1Э_Массивы.Создать(
		"РодительЭлемента",
		"ПередЭлементом",
		"ОписаниеТипов",
		"ПараметрыШкалы",
		"ИмяРеквизита",
		"ПутьКРеквизиту",
		"ЗначениеРеквизита",
		"Действия",
		"ПараметрыСписка"
	);
КонецФункции

Функция ЗначенияПоУмолчанию() Экспорт
	Возврат А1Э_Структуры.Создать(
	"Заголовок", "Этап",
	"ОтображатьШкалу", Ложь,
	"Заморозка", Ложь,
	"ПараметрыШкалы", А1Э_Структуры.Создать(
	"Цвет", А1Э_Массивы.Создать(10, 220, 60),
	"ЦветАктивный", А1Э_Массивы.Создать(0, 250, 250),
	"ЦветВторой", А1Э_Массивы.Создать(255, 255, 255)
	)
	);
КонецФункции
#КонецОбласти

#Область ДляФормы
Функция УстановитьНастройкиШкалы(Форма, ИмяКомпонента, Настройки) Экспорт
	НастройкиШкалы = Форма[ИмяКомпонента + "Шкала___Настройки"];
	ЗаполнитьЗначенияСвойств(НастройкиШкалы, Настройки);
КонецФункции

Функция ЗаморозитьЭтап(Форма, ИмяКомпонента = "СписокЭтапов") Экспорт
	Настройки = Форма[ИмяКомпонента + "___Настройки"];
	Если Настройки.Заморозка Тогда
		Возврат Неопределено;
	КонецЕсли;
	Настройки.Заморозка = Истина;
	ИмяРеквизита = А1Э_Структуры.ЗначениеСвойства(Настройки, "ИмяРеквизита");
	Если ИмяРеквизита = Неопределено Тогда
		ИмяРеквизита = ИмяКомпонента + "___Этап";
	КонецЕсли;
	Элемент = Форма.Элементы[ИмяРеквизита];
	Элемент.ТолькоПросмотр = Истина;
	Элемент.КнопкаВыпадающегоСписка = Ложь;
	Если НЕ Настройки.ОтображатьШкалу Тогда
		Возврат Неопределено;
	КонецЕсли;
	УстановитьНастройкиШкалы(
	Форма,
	ИмяКомпонента,
	А1Э_Структуры.Создать(
	"Цвет", А1Э_Массивы.Создать(120, 0, 0),
	"ЦветАктивный", А1Э_Массивы.Создать(250, 0, 0),
	"ЦветВторой", А1Э_Массивы.Создать(100, 100, 100),
	"ЦветГраницы", А1Э_Массивы.Создать(40, 40, 40)
	)
	);
	А1БК_ШкалаПрогресса.ОбновитьШкалу(Форма, ИмяКомпонента + "Шкала", Неопределено);
КонецФункции

Функция РазморозитьЭтап(Форма, ИмяКомпонента = "СписокЭтапов") Экспорт
	Настройки = Форма[ИмяКомпонента + "___Настройки"];
	Если НЕ Настройки.Заморозка Тогда
		Возврат Неопределено;
	КонецЕсли;
	Настройки.Заморозка = Ложь;
	ИмяРеквизита = А1Э_Структуры.ЗначениеСвойства(Настройки, "ИмяРеквизита");
	Если ИмяРеквизита = Неопределено Тогда
		ИмяРеквизита = ИмяКомпонента + "___Этап";
	КонецЕсли;
	Элемент = Форма.Элементы[ИмяРеквизита];
	Элемент.ТолькоПросмотр = Ложь;
	Элемент.КнопкаВыпадающегоСписка = Истина;
	Если НЕ Настройки.ОтображатьШкалу Тогда
		Возврат Неопределено;
	КонецЕсли;
	УстановитьНастройкиШкалы(Форма, ИмяКомпонента, Настройки.ПараметрыШкалы);
	А1БК_ШкалаПрогресса.ОбновитьШкалу(Форма, ИмяКомпонента + "Шкала", Неопределено);
КонецФункции

Функция НомерТекущегоЭтапа(Форма, ИмяКомпонента = "СписокЭтапов") Экспорт
	НастройкиКомпонента = Форма[ИмяКомпонента + "___Настройки"];
	Список = НастройкиКомпонента.СписокВыбора;
	ПутьКРеквизиту = А1Э_Структуры.ЗначениеСвойства(НастройкиКомпонента, "ПутьКРеквизиту");
	ИмяРеквизита = ?(А1Э_Структуры.Свойство(НастройкиКомпонента, "ИмяРеквизита"), НастройкиКомпонента.ИмяРеквизита, ИмяКомпонента + "___Этап");
	ТекущийЭтап = ?(ПутьКРеквизиту = Неопределено, Форма[ИмяРеквизита], Форма[ПутьКРеквизиту][ИмяРеквизита]);
	Возврат Список.Найти(ТекущийЭтап);
КонецФункции
#КонецОбласти

#Область НастройкиПараметров
Функция НастройкиПараметров() Экспорт
	Возврат А1Э_Структуры.Создать(
		"Обязательные", ОбязательныеПараметры(),
		"ПоУмолчанию", ЗначенияПоУмолчанию(),
		"Дополнительные", ДополнительныеПараметры()
	);
КонецФункции
#КонецОбласти