Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, Знач Колонки, Знач СохраняемыеКолонки, Заголовок = "", РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	СохраняемыеКолонки = А1Э_Массивы.Массив(СохраняемыеКолонки);
	Колонки = А1Э_Формы.ДанныеКолонокТаблицыФормы(Колонки);
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, А1Э_Структуры.Создать(
	"СохраняемыеКолонки", СохраняемыеКолонки,
	));
	Для Каждого Колонка Из Колонки Цикл
		Если НЕ А1Э_Массивы.Содержит(СохраняемыеКолонки, Колонка.Имя) Тогда Продолжить; КонецЕсли;
		
		РабочиеДействияКолонки = А1Э_Структуры.Скопировать(Колонка.Действия);
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействияКолонки, ИмяМодуля(), "ПриИзменении");
		Колонка.Действия = РабочиеДействияКолонки;
	КонецЦикла;
	
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	А1БК_Общее.ДобавитьОбработчикВключенияСохраненияВНастройках(РабочиеДействия);
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПередЗагрузкойДанныхИзНастроекНаСервере");
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПриСохраненииДанныхВНастройкахНаСервере");
	
	А1Э_Формы.ДобавитьОписаниеТаблицыФормы(МассивОписаний, ИмяКомпонента, Колонки, Заголовок, РодительЭлемента, ПередЭлементом, Параметры, РабочиеДействия); 
КонецФункции 

Функция ФормаПередЗагрузкойДанныхИзНастроекНаСервере(ИмяКомпонента, Форма, Настройки) Экспорт 
	Если Форма[ИмяКомпонента].Количество() > 0 Тогда 
		Сообщить("Для компонента " + ИмяКомпонента + " загрузка данных из настроек не выполнена: в таблице уже есть данные!");
		Возврат Неопределено;
	КонецЕсли;
	Если ТипЗнч(Настройки[ИмяКомпонента]) <> Тип("Массив") Тогда Возврат Неопределено; КонецЕсли; 	
	Для Каждого Элемент Из Настройки[ИмяКомпонента] Цикл
		СтрокаТаблицы = Форма[ИмяКомпонента].Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Элемент);
	КонецЦикла;
КонецФункции

Функция ФормаПриСохраненииДанныхВНастройкахНаСервере(ИмяКомпонента, Форма, Настройки) Экспорт   
	НастройкиКомпонента = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
	СтруктураХранения = Новый Структура;
	Для Каждого Колонка Из НастройкиКомпонента.СохраняемыеКолонки Цикл
		СтруктураХранения.Вставить(Колонка);
	КонецЦикла;
	
	СохраняемыйМассив = Новый Массив;
	Настройки.Вставить(ИмяКомпонента, СохраняемыйМассив);
	Для Каждого Строка Из Форма[ИмяКомпонента] Цикл
		СтруктураСтроки = А1Э_Структуры.Скопировать(СтруктураХранения);
		СохраняемыйМассив.Добавить(СтруктураСтроки);
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, Строка);
	КонецЦикла;
КонецФункции

Функция ПриИзменении(Форма, Элемент) Экспорт
	Форма.СохраняемыеВНастройкахДанныеМодифицированы = Истина;
КонецФункции

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_СохраняемаяВНастройкахТаблица";	
КонецФункции 