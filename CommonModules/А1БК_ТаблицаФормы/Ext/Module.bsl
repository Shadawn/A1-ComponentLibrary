// Добавляет таблицу формы с дополнительным функционалом:
// 1. В настройках компонента сохраняются ПутиКДанным колонок.
// 2. При двойном клике на колонку с видом ТолькоПросмотр открывается её значение.
//
// Параметры:
//  МассивОписаний	 - Массив -  
//  Имя				 - Строка - 
//  Колонки			 - Массив - 
//  Заголовок		 - Строка - 
//  РодительЭлемента - ЭлементФормы,Строка,Неопределено - 
//  ПередЭлементом	 - ЭлементФормы,Строка,Неопределено - 
//  Параметры		 - Структура,Неопределено - 
//  Действия		 - Структура,Неопределено - 
// 
// Возвращаемое значение:
//  ОписаниеТаблицы - Структура
//
Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, Знач Колонки, Заголовок = "", РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "Выбор");
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПриСозданииНаСервере");
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, А1Э_Структуры.Создать(
	"ПутиКДанным", Новый Структура,
	));
	А1Э_Формы.ДобавитьОписаниеТаблицыФормы(МассивОписаний, ИмяКомпонента, Колонки, Заголовок, РодительЭлемента, ПередЭлементом, Параметры, РабочиеДействия);
КонецФункции

Функция ДобавитьФункциональность(МассивОписаний, ИмяКомпонента, Действия) Экспорт
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(Действия, ИмяМодуля(), "Выбор");
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(Действия, ИмяМодуля(), "ФормаПриСозданииНаСервере");
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, А1Э_Структуры.Создать(
	"ПутиКДанным", Новый Структура,
	));
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция ФормаПриСозданииНаСервере(ИмяКомпонента, Форма, Отказ, СтандартнаяОбработка) Экспорт
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Для Каждого Колонка Из А1Э_Формы.КолонкиТаблицы(Форма, ИмяКомпонента) Цикл
			Настройки.ПутиКДанным.Вставить(Колонка.Имя, Колонка.ПутьКДанным);	
		КонецЦикла;
	КонецФункции
	
#КонецЕсли
#Если Клиент Тогда
	
	Функция Выбор(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
		ИмяКомпонента = Элемент.Имя;
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Если ТипЗнч(ВыбраннаяСтрока) = Тип("Массив") Тогда
			ОсновнаяСтрока = ВыбраннаяСтрока[0];
		Иначе
			ОсновнаяСтрока = ВыбраннаяСтрока;
		КонецЕсли;
		Если Поле.ТолькоПросмотр = Истина Тогда
			ПоказатьЗначение(, Форма[ИмяКомпонента].НайтиПоИдентификатору(ОсновнаяСтрока)[А1Э_Строки.После(Настройки.ПутиКДанным[Поле.Имя], ".")]);		
		КонецЕсли;
	КонецФункции 
	
#КонецЕсли

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_ТаблицаФормы";
КонецФункции 