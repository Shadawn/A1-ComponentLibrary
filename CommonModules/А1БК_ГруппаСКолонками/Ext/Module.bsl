#Область README
//Этот компонент создаёт головную группу (горизонтальную) и несколько подчиненных групп (вертикальных).
//В обработчика <ФормаПриСозданииНаСервере> компонента элементы, добавленные в основную группу, распределяются равномерно между подчиненными, так чтобы было красиво.
//
#КонецОбласти 

// Функция - Добавить описание
//
// Параметры:
//  МассивОписаний		 - Массив - 
//  ИмяКомпонента		 - Строка - 
//  КоличествоКолонок	 - Число - 
//  Заголовок			 - Строка - 
//  РодительЭлемента	 - 	 - 
//  ПередЭлементом		 - 	 - 
//  Параметры			 - 	 - 
//  Действия			 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, КоличествоКолонок, Заголовок = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПриСозданииНаСервере");
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, А1Э_Структуры.Создать(
	"КоличествоКолонок", КоличествоКолонок,
	));
	А1Э_Формы.ДобавитьОписаниеГоризонтальнойГруппы(МассивОписаний, ИмяКомпонента, Заголовок, РодительЭлемента, ПередЭлементом, Параметры, РабочиеДействия);
	Для Сч = 1 По КоличествоКолонок Цикл
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, ИмяКомпонента + А1Э_Строки.ВСтроку(Сч), , ИмяКомпонента);
	КонецЦикла;
	
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция ФормаПриСозданииНаСервере(ИмяКомпонента, Форма, Отказ, СтандартнаяОбработка) Экспорт
	  	РаспределитьПодчиненные(Форма, ИмяКомпонента);
	КонецФункции
	
	Функция РаспределитьПодчиненные(Форма, ИмяКомпонента) Экспорт
		ГоловнойЭлемент = Форма.Элементы[ИмяКомпонента];
		
		//Разбиваем подчиненные элементы на подгруппы и все остальное (перемещаемые элементы).
		МассивПодгрупп = Новый Массив;
		МассивПеремещаемых = Новый Массив;
		Для Каждого Элемент Из ГоловнойЭлемент.ПодчиненныеЭлементы Цикл
			Если СтрНачинаетсяС(Элемент.Имя, ИмяКомпонента) Тогда 
				МассивПодгрупп.Добавить(Элемент);
			Иначе
				МассивПеремещаемых.Добавить(Элемент);
			КонецЕсли;	
		КонецЦикла;
		Если МассивПодгрупп.Количество() = 0 Тогда Возврат Неопределено; КонецЕсли;
		Если МассивПеремещаемых.Количество() = 0 Тогда Возврат Неопределено; КонецЕсли;
		
		//В подгруппах уже может что-то быть. Мы хотим распределить так, чтобы количество в каждой подгруппе было максимально равным.
		//Для этого мы выделяем те подгруппы в которых минимальное количество элементов.
		ПодгруппыПоКоличеству = Новый Соответствие;
		Для Каждого Подгруппа Из МассивПодгрупп Цикл
			А1Э_Иерархии.ДобавитьВСоответствиеМассивов(ПодгруппыПоКоличеству, Подгруппа.ПодчиненныеЭлементы.Количество(), Подгруппа);
		КонецЦикла;
		МинимальноеКоличество = 1000000000000;
		Для Каждого Пара Из ПодгруппыПоКоличеству Цикл
			Если Пара.Ключ < МинимальноеКоличество Тогда
				МинимальноеКоличество = Пара.Ключ;
			КонецЕсли;	
		КонецЦикла;
		МинимальныеПодгруппы = ПодгруппыПоКоличеству[МинимальноеКоличество];
		
		//Обходим все перемещаемые элементы и раскидываем их по подгруппам с минимальным количеством элементов. В конце каждого ряда минимальное количество увеличивается,
		//и в список подгрупп с минимальным количеством элементов могут быть добавлены новые подгруппы.
		Сч = -1;
		Для Каждого Элемент Из МассивПеремещаемых Цикл
			Сч = Сч + 1;
			Если Сч >= МинимальныеПодгруппы.Количество() Тогда
				МинимальноеКоличество = МинимальноеКоличество + 1;
				Сч = 0;
				НовыеМинимальныеПодгруппы = ПодгруппыПоКоличеству[МинимальноеКоличество];
				Если НовыеМинимальныеПодгруппы <> Неопределено Тогда
					А1Э_Массивы.Добавить(МинимальныеПодгруппы, НовыеМинимальныеПодгруппы);
					//Может оказаться, что сначала было добавление в более правую группу. Если не отсортировать, то и в дальнейшем может быть добавление в более правую группу.
					СортироватьПоИмени(Форма, МинимальныеПодгруппы);
				КонецЕсли;
			КонецЕсли;
			Форма.Элементы.Переместить(Элемент, МинимальныеПодгруппы[Сч]);
		КонецЦикла;
		
	КонецФункции
	
	Функция СортироватьПоИмени(Форма, МассивЭлементовФормы)
		МассивИмен = Новый Массив;
		Для Каждого ЭлементФормы Из МассивЭлементовФормы Цикл
			МассивИмен.Добавить(ЭлементФормы.Имя);
		КонецЦикла;
		А1Э_Массивы.Сортировать(МассивИмен);
		МассивЭлементовФормы.Очистить();
		Для Каждого Имя Из МассивИмен Цикл
			МассивЭлементовФормы.Добавить(Форма.Элементы[Имя]);
		КонецЦикла;
	КонецФункции 
	
#КонецЕсли

Функция ИмяМодуля() Экспорт
	Возврат "А1БК_ГруппаСКолонками";
КонецФункции 
 